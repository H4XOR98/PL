%{
    #include<glib.h>
    #include "y.tab.h"
    void yyerror(char* s);
%}

%option noyywrap yylineno

%x TEXTO CONT SUJ REL OBJ IMG

%%
                                            GString* conteudo = g_string_new(NULL); 

===\ \:.+/\n                                { yylval.string = strdup(yytext+5); return CONCEITO; }

@tit\:\ .+/\n                               { BEGIN TEXTO;  yylval.string = strdup(yytext+6); return TITULO; }     
                    
<TEXTO>@triplos\:                           { BEGIN SUJ; }
<TEXTO>#\ .+[\n]{2}                         { BEGIN CONT; yytext[strlen(yytext)-2] = '\0'; yylval.string = strdup(yytext+2); return SUBTITULO; }
<TEXTO>\n                                   ;

<CONT>.+/\n                                 { g_string_append(conteudo, yytext); } 
<CONT>^\n                                   { yylval.string = strdup(conteudo->str); g_string_erase(conteudo, 0, -1); return CONTEUDO; }
<CONT>\n/(#|@)                              { BEGIN TEXTO; yylval.string = strdup(conteudo->str); g_string_erase(conteudo, 0, -1); return CONTEUDO; }
<CONT>\n                                    { g_string_append(conteudo," "); }

<SUJ>\n/===\ \:                             { BEGIN 0; }
<SUJ>\:.+/\n                                { BEGIN REL; yylval.string = strdup(yytext+1); return SUJEITO; }
<SUJ>\n                                     ;

<REL>\:img/\                                { BEGIN IMG; }
<REL>a/\                                    { BEGIN OBJ; yylval.string = strdup(yytext); return RELACAO; }
<REL>\:[^ ]*/\                              { BEGIN OBJ; yylval.string = strdup(yytext+1); return RELACAO; }
<REL>[\n;, ]                                ;

<OBJ>\.                                     { BEGIN SUJ; }
<OBJ>;                                      { BEGIN REL; }
<OBJ>\:[^ ,;.]*/(\ |,|;|\.)                 { yylval.string = strdup(yytext+1); return OBJECTO; }
<OBJ>\".*/\"                                { yylval.string = strdup(yytext+1); return OBJECTO; }
<OBJ>[ ,\n\"]                               ;

<IMG>\.                                     { BEGIN SUJ; }
<IMG>;                                      { BEGIN REL; }
<IMG>\".*/\"                                { yylval.string = strdup(yytext+1); return IMAGEM; }
<IMG>[ ,\n\"]                               ;

.|\n                                        ;

%%